# Copyright (c) 2023 Huawei Technologies Co., Ltd. All rights reserved.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/ft_build/ark_config.gni")

isa_paths = [ rebase_path("$ark_root/isa/isa.yaml", root_build_dir) ]

foreach(plugin, enabled_plugins) {
  print("add isa plugin: $plugin")
  plugin_dir = "$ark_root/plugins/$plugin"

  source_files = read_file("$plugin_dir/subproject_sources.gn", "scope")
  if (defined(source_files.srcs_isa_path)) {
    source_file = "$plugin_dir/${source_files.srcs_isa_path}"
    src_scope = read_file(source_file, "scope")

    if (defined(src_scope.srcs)) {
      foreach(src, src_scope.srcs) {
        isa_paths += [ rebase_path("$ark_root/$src", root_build_dir) ]
      }
    }
  }

  source_files = {
  }
  src_scope = {
  }
}

action("isa_combine") {
  script = "$ark_root/isa/combine.rb"
  input_args = string_join(",", isa_paths)
  inputs = [ "$ark_root/isa/isa.yaml" ]
  outputs = [ "$root_gen_dir/isa/isa.yaml" ]
  args = [
    "-d",
    "$input_args",
    "-o",
    rebase_path(outputs[0], root_build_dir),
  ]
}
