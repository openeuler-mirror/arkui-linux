# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_runtime/js_runtime_config.gni")
import("//arkcompiler/ets_runtime/test/test_helper.gni")

test_js_path =
    "//arkcompiler/ets_runtime/ecmascript/debugger/tests/single_file/"

test_js_files = [
  "base/index",
  "patch/index",
]

module_output_path = "arkcompiler/ets_runtime"

foreach(file, test_js_files) {
  es2abc_gen_abc("gen_${file}_abc") {
    test_js = "${test_js_path}${file}.js"
    test_abc = "$target_out_dir/single_file/${file}.abc"

    # Only targets in this file can depend on this.
    extra_visibility = [ ":*" ]
    src_js = rebase_path(test_js)
    dst_file = rebase_path(test_abc)
    extra_args = [ "--module" ]
    extra_args += [ "--merge-abc" ]

    in_puts = [ test_js ]
    out_puts = [ test_abc ]
  }
}

host_unittest_action("HotReloadManagerTest") {
  module_out_path = module_output_path

  sources = [
    # test file
    "hot_reload_manager_test.cpp",
  ]

  configs = [
    "//arkcompiler/ets_runtime:ecma_test_config",
    "$ark_root/assembler:arkassembler_public_config",
    "$ark_root/libpandafile:arkfile_public_config",
  ]

  deps = [
    "//arkcompiler/ets_runtime:libark_jsruntime_test",
    "//arkcompiler/runtime_core/assembler:libarkassembler_static",
    sdk_libc_secshared_dep,
  ]

  foreach(file, test_js_files) {
    deps += [ ":gen_${file}_abc" ]
  }

  if (is_ohos && is_standard_system) {
    test_abc_dir = "/data/test"
  } else {
    test_abc_dir = rebase_path(target_out_dir)
  }

  test_js_dir = rebase_path(test_js_path)

  defines = [
    "DEBUGGER_ABC_DIR=\"${test_abc_dir}/\"",
    "DEBUGGER_JS_DIR=\"${test_js_dir}\"",
  ]
}

group("unittest") {
  testonly = true

  # deps file
  deps = [ ":HotReloadManagerTest" ]
}

group("host_unittest") {
  testonly = true

  # deps file
  deps = [ ":HotReloadManagerTestAction" ]
}
