# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

# build core sources
template("ace_core_apng_source_set") {
  forward_variables_from(invoker, "*")

  source_set(target_name) {
    defines += invoker.defines

    #add png support
    include_dirs = [
      "$flutter_root/skia/third_party/libpng",
      "$flutter_root/skia/third_party/externals/libpng",
      "//third_party/zlib",
    ]

    # apng support file
    sources = [
      "apng_image_animation.cpp",
      "apng_image_decoder.cpp",
      "apng_image_object.cpp",
      "apng_image_player.cpp",
    ]

    configs += [ "$ace_root:ace_config" ]
    defines += [ "APNG_IMAGE_SUPPORT" ]

    deps = [
      "$ace_flutter_engine_root:third_party_flutter_engine_$platform",
      "$ace_flutter_engine_root/skia:ace_skia_$platform",
      "../../pipeline:ace_core_pipeline_$platform",
      "../../pipeline_ng:ace_core_pipeline_ng_$platform",
    ]

    cflags_cc = []
    cflags_cc += invoker.cflags_cc
  }
}

foreach(item, ace_platforms) {
  ace_core_apng_source_set("ace_core_apng_" + item.name) {
    platform = item.name

    if (defined(item.config)) {
      config = item.config
    } else {
      config = {
      }
    }

    if (defined(config.defines)) {
      defines = config.defines
    } else {
      defines = []
    }

    if (defined(config.cflags_cc)) {
      cflags_cc = config.cflags_cc
    } else {
      cflags_cc = []
    }
  }
}
