# Copyright (c) 2021 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/test.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")

module_output_path = "ace_engine_full/backenduicomponent/rating"

ohos_unittest("RatingCreatorTest") {
  module_out_path = module_output_path

  sources = [ "rating_creator_test.cpp" ]

  configs = [
    ":config_json_creator_test",
    "$ace_root:ace_test_config",
  ]

  deps = [
    "$ace_root/build:ace_ohos_unittest_base",
    "$ace_root/frameworks/core/components/test:json",
  ]
  part_name = ace_engine_part
}

config("config_json_creator_test") {
  visibility = [ ":*" ]
  include_dirs = [ "$ace_root" ]
}

ohos_unittest("RatingComponentTest") {
  module_out_path = module_output_path

  sources = [
    "$ace_root/frameworks/base/log/dump_log.cpp",
    "$ace_root/frameworks/core/components/common/properties/color.cpp",
  ]

  if (is_standard_system) {
    sources += []
  } else {
    sources += [ "$ace_root/frameworks/core/components/test/mock/mock_resource_adapter.cpp" ]
  }

  # add sources only needed by wearable like watch.
  if (is_wearable_product) {
    sources += [ "$root_out_dir/arkui/framework/core/components/theme/theme_constants_watch.cpp" ]
  }

  # add sources needed by phone and TV. wearable like watch do not need them
  if (!is_wearable_product) {
    sources += [
      # theme
      "$root_out_dir/arkui/framework/core/components/theme/theme_constants_tv.cpp",
    ]
  }

  configs = [
    ":config_render_rating_test",
    "$ace_root:ace_test_config",
    "$ace_flutter_engine_root:flutter_config",
  ]

  deps = [
    "$ace_flutter_engine_root/skia:ace_skia_ohos",
    "$ace_root/adapter/ohos/osal:ace_osal_ohos",
    "$ace_root/frameworks/core/components/theme:build_theme_code",
    "$cjson_root:cjson_static",
    "//third_party/googletest:gtest_main",
  ]

  if (!is_standard_system) {
    external_deps = [
      "hilog:libhilog",
      "resmgr:resourcemanager",
    ]
    deps += [ "$ace_root/frameworks/base:ace_base_ohos" ]
  } else {
    external_deps = [ "hiviewdfx_hilog_native:libhilog" ]
    deps +=
        [ "//base/global/resource_management/frameworks/resmgr:global_resmgr" ]
  }

  external_deps += [ "c_utils:utils" ]

  part_name = ace_engine_part
}

config("config_render_rating_test") {
  visibility = [ ":*" ]
  include_dirs = [
    "//commonlibrary/c_utils/base/include",
    "$ace_root",
  ]
}

group("unittest") {
  testonly = true
  deps = [
    # ":RatingComponentTest",
    ":RatingCreatorTest",
  ]
}
