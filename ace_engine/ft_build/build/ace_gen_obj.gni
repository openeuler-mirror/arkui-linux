# Copyright (c) 2023 Huawei Technologies Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/gn/fangtian.gni")
import("//ace_engine/ft_build/ace_config.gni")

template("gen_obj") {
  name = target_name
  action("gen_obj_" + name) {
    visibility = [ ":*" ]  # Only targets in this file can depend on this.

    if (use_linux) {
      objcopy_tool = objcopy_x86_64
      script = "$ace_root/ft_build/build/tools/build_resource_to_bytecode.py"
    } else if (target_cpu == "x86_64") {
      objcopy_tool = objcopy_x86_64
      script = "$flutter_root/engine/flutter/sky/tools/objcopy.py"
    } else {
      assert(false, "Unsupported target_cpu: " + target_cpu)
    }

    args = [
      "--objcopy",
      rebase_path(objcopy_tool),
      "--input",
      rebase_path(invoker.input),
      "--output",
      rebase_path(invoker.output),
      "--arch",
      current_cpu,
    ]

    deps = []
    deps += invoker.snapshot_dep

    inputs = [ invoker.input ]
    outputs = [ invoker.output ]
  }

  source_set("gen_obj_src_" + name) {
    sources = [ invoker.output ]
    deps = [ ":gen_obj_" + name ]
  }
}
