# Copyright (c) 2023 Huawei Technologies Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/gn/fangtian.gni")
import("//ace_engine/ft_build/ace_config.gni")

# build static
template("utilsecurec_source") {
  forward_variables_from(invoker, "*")
  ft_source_set(target_name) {
    defines += invoker.defines
    # configs = [ "//third_party/bounds_checking_function:libsec_public_config" ]
    deps = [ "//build/gn/configs/system_libs:c_utils" ]
  }
}

# build static
template("ace_osal_preview_source_set") {
  forward_variables_from(invoker, "*")

  ft_source_set(target_name) {
    defines += invoker.defines

    sources = [
      "log_wrapper.cpp",
      "ace_engine_ext.cpp",
      "ace_trace.cpp",
      "event_report.cpp",
      "exception_handler.cpp",
      "fetch_manager.cpp",
      "frame_report.cpp",
      "mouse_style_ohos.cpp",
      "pixel_map_preview.cpp",
      "response_data.cpp",
      "ressched_report.cpp",
      "stage_card_parser.cpp",
      "system_properties.cpp",
      "trace_id_impl.cpp",
    ]

    cflags_cc = [
      "-DNAME_MAX=128",
      "-Wno-inconsistent-dllimport",
      "-Wno-unused-const-variable",
      "-Wno-unused-function",
      "-Wno-missing-declarations",
    ]

    deps = [
      ":utilsecurec_source_$platform",
      "//build/gn/configs/system_libs:mmi",
      "//build/gn/configs/system_libs:eventhandler",
    ]

    libs = ["curl"]
    if (is_standard_system) {
      sources += [
        "${ace_root}/adapter/fangtian/osal/resource_theme_style.cpp",
        "frame_trace_adapter_impl.cpp",
        "resource_adapter_impl_standard.cpp",
        "resource_convertor.cpp",
      ]
      include_dirs = [
        # Fangtian build
        "//foundation/window/window_manager/previewer/mock",
        "//commonlibrary/c_utils/base/include"
      ]
      defines += [ "OHOS_STANDARD_SYSTEM" ]

      if (platform == "fangtian") {
        # Fangtian TODO
        deps += [ "//resource_management/frameworks/resmgr/ft_build:global_resmgr_linux(${current_toolchain})" ]
      } else {
        assert(false, "not support platform")
      }
    } else {
      sources += [
        "frame_trace_adapter_impl.cpp",
        "resource_adapter_impl.cpp",
      ]
      include_dirs = [
        "//commonlibrary/c_utils/base/include",
        "//prebuilts/ace-toolkit/preview/rich/include/resmgr/resourcemanager/include",
      ]
      if (platform == "windows") {
        defines += [ "NOGDI" ]
        libs = [ "//prebuilts/ace-toolkit/linux/rich/lib/windows/tv/libresourcemanager_win.lib" ]
      } else if (platform == "mac") {
        libs = [ "//prebuilts/ace-toolkit/linux/rich/lib/mac/tv/libresourcemanager_mac.dylib" ]
      } else if (platform == "linux") {
        libs = [ "//prebuilts/ace-toolkit/linux/rich/lib/linux/tv/libresourcemanager_linux.so" ]
      }
    }

    configs = [
      "$ace_root/ft_build:ace_config",
    ]
  }
}

foreach(item, ace_platforms) {
  platform = item.name
  if (platform == "windows" || platform == "mac" || platform == "linux" || platform == "fangtian") {
    utilsecurec_source("utilsecurec_source_" + item.name) {
      # platform = item.name
      defines = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }
      if (defined(config.defines)) {
        defines = config.defines
      }
    }
    ace_osal_preview_source_set("ace_osal_" + item.name) {
      platform = item.name
      defines = []
      config = {
      }

      if (defined(item.config)) {
        config = item.config
      }
      if (defined(config.defines)) {
        defines = config.defines
      }
    }
  }
}
